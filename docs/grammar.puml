@startebnf
title Lite Pascal Grammar 
(*Future implementations are in dashed blocks*)

LitePascal = program | unit | ?Library?;

Program = ["program", Ident, ";"], [UsesClause], Block, ".";
 
Library = ?"library"?, ?Ident?, ?";"?, [?UsesClause?], {?Declarations?}, ?"exports"?, ?IdentList?, ?";"?, (?CompoundStatement? | ?"end"?), ?"."?;
 
Unit = "unit", Ident, ";", "interface", [Uses], {Declarations}, 
       "implementation", [Uses], Block, ".";
                
Uses = "uses", Ident, {",", Ident}, ";";

Block = {Declarations}, (CompoundStatement | "end");

Declarations = Labels | Consts | Types | Vars | Procedure | Function;
                                    
Labels = "label", Ident, {",", Ident}, ";";               
             
Consts = "const", {UntypedConsts | TypedConsts}-;

UntypedConsts = Ident, "=", ConstExpression, ";";
                                 
TypedConsts = Ident, ":", Type, "=", Initializer, ";";

Initializer = ConstExpression | StringLiteral |
              "(", Initializer, {",", Initializer}, ")" |
              "(", Ident, ":", Initializer, {";", Ident, ":", Initializer}, ")" |
              SetConstructor;                     

Types = "type", {Ident, "=", Type, ";"}-;

Vars = ("var" | ?"threadvar"?), {IdentList, ":", Type, ["=", Initializer], ";"}-;

Procedure = "procedure", [?TypeIdent?, ?"."?], Ident, [?Generics?], [FormalParams], [CallModifier], ";", [(Directive | Block | ?"=", SimpleStatement?), ";"];

Function = "function", [?TypeIdent?, ?"."?], Ident, [?Generics?], [FormalParams], ":", TypeIdent, [CallModifier], ";", [(Directive | Block | ?"=", Expression?), ";"];

Generics = ?"<"?, ?TypeIdent?, {?","?, ?TypeIdent?}, ?">"?;


CallModifier = "stdcall" | "cdecl";

Directive = "forward" | "external", ConstExpression | ?"inline"?;

ActualParams = "(", [(Expression | Designator), {",", (Expression | Designator)}], ")";

FormalParams = "(", FormalParamList, {";", FormalParamList}, ")";
              
FormalParamList = ["const" | "var" | ?"out"?], IdentList, [":", ["array", "of"], TypeIdent], ["=", ConstExpression];             

IdentList = Ident, {",", Ident};

Type = "(", Ident, {",", Ident}, ")" | "^", TypeIdent |
       ["packed"], "array", "[", Type, {",", Type}, "]", "of", Type |
       ["packed"], "record", [?"(", TypeIdent, ")"?], Fields, "end" |
       "interface", [?"(", TypeIdent, {", TypeIdent}, ")"?], {?Prototypes?}, "end" |
       ["packed"], "set", "of", TypeIdent |
       ["packed"], "string", ["[", ConstExpression, "]"] |
       ["packed"], "file", ["of", TypeIdent] |
       ConstExpression, "..", ConstExpression |
       ("procedure" | "function"), [FormalParams], [":", TypeIdent], [CallModifier] |
       Ident;
       
Prototypes = ("procedure" | "function"), Ident, [?Generics?], [FormalParams], [":", TypeIdent], ";";

Fields = FixedFields, ["case", [Ident, ":"], Type, "of", CaseField, {";", CaseField}], [";"];       
 
CaseField = CaseLabel, {",", CaseLabel}, ":", "(", Fields, ")";

CaseLabel = ConstExpression, ["..", ConstExpression];

FixedFields = IdentList, ":", Type, {";", IdentList, ":", Type};       
       
TypeIdent = "string" | "file" | Ident, [?Generics?];       

Designator = (Ident | TypeIdent, "(", Expression, ")"), {Selector};

Selector = "^" | "[", Expression, {",", Expression}, "]" | ".", Ident | ActualParams;

Statement = Ident, ":" | SimpleStatement | CompoundStatement | If | Case | While | Repeat | For | Goto | With;                                    

SimpleStatement = Designator, [(":="|?"+="?|?"-="?|?"*="?|?"/="?), Expression];

StatementList = Statement, {";", Statement};

CompoundStatement = "begin", StatementList, "end";

If = "if", Expression, "then", Statement, ["else", Statement];

Case = "case", Expression, "of", CaseElement, {";", CaseElement}, [";"], ["else", StatementList], [";"], "end";
                    
While = "while", Expression, "do", Statement;

Repeat = "repeat", StatementList, "until", Expression;

For = "for", [?"var"?], Ident, ":=", Expression, ("to" | "downto"), Expression, "do", Statement;

Goto = "goto", Ident;

With = "with", Designator, {",", Designator}, "do", Statement;                    
 
CaseElement = CaseLabel, {",", CaseLabel}, ":", Statement;

ConstExpression = Expression;

Expression = SimpleExpression, [("="|"<>"|"<"|"<="|">"|">="|"in"|?"is"?|?"as"?), SimpleExpression];

SimpleExpression = ["+"|"-"], Term ,{("+"|"-"|"or"|"xor"), Term} | 
                   ?"if"?, ?Expression?, ?"then"?, ?Expression?, ?"else"?, ?Expression?;

Term = Factor, {("*"|"/"|"div"|"mod"|"shl"|"shr"|"and"), Factor};

Factor = ["@"], Designator | Number | CharLiteral | StringLiteral | 
         "(", Expression, ")" | "not", Factor | SetConstructor | "nil";
         
SetConstructor = "[", [Expression, ["..", Expression], {",", Expression, ["..", Expression]}], "]";         

Ident = (Letter | "_"), {Letter | "_" | Digit};

Number = "$", {HexDigit}- | {Digit}-, [".", {Digit}-], ["e", ["+" | "-"], {Digit}-];

CharLiteral = "'", (Character | "'", "'"), "'" | "#", Number | ?"^", Letter?;

StringLiteral = "'", {Character | "'", "'"}, "'" | ?"#", Number? | ?"^", Letter? | StringLiteral;
<style>
 element {
   ebnf {
     LineColor blue
     Fontcolor blue
     Backgroundcolor #FEFEFE/#B6D8EB
     note {Backgroundcolor yellow}
   }
 }
</style>
@endebnf
